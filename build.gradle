apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'clean', 'build', 'install'

group = 'org.helios'
version = '0.0.4'

ext.isReleaseVersion = !version.endsWith("-RC")

ext {
    if (!project.hasProperty('ossrhUsername')) {
        ossrhUsername = ''
    }

    if (!project.hasProperty('ossrhPassword')) {
        ossrhPassword = ''
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://sevntu-checkstyle.github.com/sevntu.checkstyle/maven2"
    }
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


compileJava {
    // Suppress warnings about using Unsafe and sun.misc
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    checkstyle "com.github.sevntu.checkstyle:sevntu-checks:1.17.1"

    compile(
        'org.hdrhistogram:HdrHistogram:2.1.9',
        'org.agrona:Agrona:0.5.5',
        'io.aeron:aeron-client:1.0.2',
        'io.aeron:aeron-driver:1.0.2',
        'uk.co.real-logic:sbe-tool:1.5.2',
        'com.lmax:disruptor:3.3.6',
        'com.vlkan:flatbuffers:1.2.0-3f79e055')

    testCompile(
        'org.hamcrest:hamcrest-all:1.3',
        'junit:junit:4.12',
        'org.mockito:mockito-all:1.10.19')
}

checkstyle {
    configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
    toolVersion = "6.14.1"
}

javadoc {
    title = '<h1>Helios</h1>'

    options.bottom = '<i>Copyright &#169; 2016. All Rights Reserved.</i>'
    options.addStringOption('XDignore.symbol.file', '-quiet')
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task copyDependencies(type: Copy) {
    from configurations.runtime
    into "${projectDir}/lib"
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name = 'Helios'
                packaging = 'pom'
                // optionally artifactId can be defined here
                description = 'Flexible and lightweight component library for domain-specific scalable applications.'
                url = 'https://github.com/canepat/Helios'

                scm {
                    connection = 'scm:git:https://github.com/canepat/Helios.git'
                    developerConnection = 'scm:git:https://github.com/canepat/Helios.git'
                    url = 'https://github.com/canepat/Helios.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'canepat'
                        name = 'Tullio Canepa'
                        email = 'tullio.canepa@gmail.com'
                        url = 'https://github.com/canepat'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
